grammar org.xtext.example.mydsl.SeleniumDriver with org.eclipse.xtext.common.Terminals

generate seleniumDriver "http://www.xtext.org/example/mydsl/SeleniumDriver"

TestSuite:
	'TestSuite' suiteName=STRING '{'
		cases+=TestCase*
	'}';

TestCase:
	'TestCase' caseName=STRING '{'
		actions+=Action*
	'}';
	
Action:
	elem=Element'.'command=Command'('param=Parameter?')';

Command:
	actionCmd=ActionCommandType | VerifyCmd=VerifyCommandType;
	
Element:
	globalElement=GlobalElementType | WebElement;

WebElement:
	type=WebElementType('('selector=Selector')');

Selector:
	property=PropertyType | Attributes;
	
Attributes: 
	attrs += Attribute (',' attrs += Attribute)*;

Attribute:
	attType=ID':'attVal=STRING;
	
Parameter:
	param=STRING | WebElement;
	
PropertyType:
	"ALL" | "FIRST";

GlobalElementType:
	"browser" | "page";

WebElementType: 
	"link" | "button" | "field" | "image" | "div";
	
ActionCommandType:
	"open" | "click" | "goTo" | "contains" | "fill";

VerifyCommandType:
	"verify";