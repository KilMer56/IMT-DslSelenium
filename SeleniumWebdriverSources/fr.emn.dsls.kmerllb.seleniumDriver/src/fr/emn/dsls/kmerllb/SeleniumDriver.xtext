grammar org.xtext.example.mydsl.SeleniumDriver with org.eclipse.xtext.common.Terminals

generate seleniumDriver "http://www.xtext.org/example/mydsl/SeleniumDriver"

TestSuite:
	'TestSuite' suiteName=ID '{'
		cases+=TestCase*
	'}';

TestCase:
	'TestCase' caseName=ID '{'
		lines+=Line*
	'}';
	
Line:
	VariableAssignation | Action;

Variable:
	name=ID;
	
VariableRef:
	ref=[Variable];

VariableAssignation:
	var=Variable '=' value=WebElement;
	
Action:
	elem=Element'.'command=Command'('param=Parameter?')';

Command:
	actionCmd=ActionCommandType | VerifyCmd=VerifyCommandType;
	
Element:
	globalElement=GlobalElementType | WebElement | VariableRef;

WebElement:
	type=WebElementType('('selector=Selector')')('.'actionCmd=ActionCommandType)?('['index=INT']')?('.'attribute=ID)?;

Selector:
	property=PropertyType | Attributes;
	
Attributes: 
	attrs += Attribute (',' attrs += Attribute)*;

Attribute:
	attType=ID':'value=AttributeValue;

AttributeValue:
	val=STRING | VariableRef('.'attribute=STRING)?;
	
Parameter:
	param=STRING | WebElement | VariableRef;
	
PropertyType:
	"ALL" | "FIRST";

GlobalElementType:
	"browser" | "page";

WebElementType: 
	"link" | "button" | "field" | "image" | "div" | "checkbox" | "title" | "combobox";
	
ActionCommandType:
	"open" | "click" | "goTo" | "contains" | "write" | "select" | "check" | "uncheck" | "parent";

VerifyCommandType:
	"verify";